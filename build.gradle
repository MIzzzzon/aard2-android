
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0+'
    }
}

apply plugin: 'android'
apply plugin: 'eclipse'
apply plugin: 'idea'

sourceCompatibility = '1.7'

repositories {
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
}

android {
    compileSdkVersion 19
    buildToolsVersion "21.1.2"

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        project.file("exclude.txt").eachLine {
            line ->
                println line
                if (line && line.trim() && !line.startsWith("#")) {
                    exclude line.trim()
                }
        }
    }


    lintOptions {
        abortOnError false
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }

    if (System.getenv("KEYSTORE") != null) {
        signingConfigs {
            release {
                storeFile file(System.getenv("KEYSTORE"))
                storePassword new String(System.console().readPassword("\n\$ Keystore password: "))
                keyAlias "aard2-android"
                keyPassword new String(System.console().readPassword("\n\$ Key password: "))
            }
        }
    }

    buildTypes {
        if (System.getenv("KEYSTORE") != null) {
            release {
                signingConfig signingConfigs.release
            }
        }
    }
}

dependencies {
    compile 'com.fasterxml.jackson.core:jackson-databind:2.4+'
    compile 'com.fasterxml.jackson.core:jackson-core:2.4+'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.4+'
    compile 'com.shamanland:fonticon:0.1.8'
    compile project(':slobj')
    compile project(':slobber')
    compile project(':slobber').files('libs/simple-5.1.7.1.itkach.jar')
    compile files('libs/android-support-v13.jar')
}
